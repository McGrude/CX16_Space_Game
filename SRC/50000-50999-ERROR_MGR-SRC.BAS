50000 REM === START ERROR MANAGER LIBRARY ===
50001 REM PRINTS ERROR MESSAGES AND CODES, 
50002 REM CAN STOP EXCUTION
50003 REM 
50004 REM VARIABLES DEFINED :
50005 REM   ER% - ERROR CODE
50006 REM   ER$ - ERROR STRING
50007 REM   EL% - LINE NUMBER WHERE ERROR OCCURRED
50008 REM   EM$ - MODULE NAME WHERE ERROR OCCURRED
50009 REM 
50010 REM SIDE EFFECTS :
50011 REM   CHANGES FORGROUND AND BACKGROUND COLORS
50012 REM   MOVES CURSOR
50013 REM   STOPS EXECUTION ( IF ERROR-MGR-STOP CALLED )
50014 REM   ENDS EXECUTION ( IF ERROR-MGR-END CALLED )
50099 END

50100 REM === PUBLIC INTERFACES ===
50110 GOSUB 50210 : RETURN : REM ERROR-MGR-INIT
50120 GOSUB 50310 : RETURN : REM ERROR-MGR-SHOW
50130 GOSUB 50410 : RETURN : REM ERROR-MGR-STOP
50140 GOSUB 50510 : RETURN : REM ERROR-MGR-END

50200 REM === ERROR-MGR-INIT
50210 ER%=0 : ER$="" : REM ERROR CODE, ERROR STRING
50220 EL%=0 : EM$="" : REM LINE NUM, MODULE NAME
50230 RETURN

50300 REM === ERROR-MGR-SHOW
50310 LOCATE 59,1 : REM LOWER LEFT CORNER
50320 COLOR 8,0  : REM RED ON BLACK
50330 PRINT " ERROR:";ER%;" '";ER$;"' : AT LINE ";EL%;" IN ";EM$;" ";
50340 RETURN

50400 REM === ERROR-MGR-STOP
50410 GOSUB 50120 : STOP : RETURN

50500 REM === ERROR-MGR-END
50510 GOSUB 50120 : END
