51000 REM ============================================
51002 REM WINDOW SYSTEM: VARIABLES AND SUBROUTINES
51004 REM ============================================
51006 REM EXTERNAL VARIABLES:
51008 REM   AN(0) = WINDOW LEFT X (COLUMN, 1-BASED)
51010 REM   AN(1) = WINDOW TOP Y  (ROW, 1-BASED)
51012 REM   AN(2) = WINDOW WIDTH  (>=3)
51014 REM   AN(3) = WINDOW HEIGHT (>=4)
51016 REM   AN(4) = TITLE FLAG (0=AUTO, 1=FORCE TITLE BAR)
51018 REM   AN(5) = FILL FLAG (0=NO FILL, 1=FILL INTERIOR WITH SPACES)
51020 REM   AN(6) = CORNER FLAG (0=SQUARE, 1=ROUNDED)
51022 REM   AS$(0) = WINDOW TITLE (MUTATED, MAY BE TRUNCATED/PADDED)
51024 REM   ER = ERROR CODE (0 = OK)
51026 REM   ER$ = ERROR STRING
51028 REM INTERNAL AN() SCRATCH:
51030 REM   AN(7) = WINDOW RIGHT X (AN(0)+AN(2)-1)
51032 REM   AN(8) = WINDOW BOTTOM Y (AN(1)+AN(3)-1)
51034 REM   AN(9) = TITLE WIDTH (AN(2)-2)
51036 REM INTERNAL VARIABLES:
51038 REM   WD() = BORDER GLYPHS, INDEX 0-8
51040 REM   X,Y = LOOP INDICES / COORDINATES
51042 REM   L,R,M = LEFT/RIGHT/MIDDLE GLYPHS
51044 REM   YT,SS,YS,FI = TITLE ROW, SIDE START, FILL START, FILL COUNT
51046 REM   T1$,C$ = TITLE BUILD STRING, TEMP CHAR
51048 REM SUBROUTINES:
51050 REM   51062 WD-INIT         : INIT WD(4..8), RESET ER/ER$
51052 REM   51084 WIN-SET-SQUARE  : INTERNAL, CALLED BY WIN-FRAME-DRAW
51054 REM   51100 WIN-SET-ROUNDED : INTERNAL, CALLED BY WIN-FRAME-DRAW
51056 REM   51124 WIN-FRAME-DRAW  : DRAW FRAME, OPTIONAL TITLE AND FILL
51058 REM   51784 WIN-HLINE       : DRAW HORIZONTAL LINE AT Y USING L,R,M

51062 REM ============================================
51064 REM WD-INIT: BORDER STATIC GLYPHS
51066 REM ============================================
51068 DIM WD(8)
51070 ER=0:ER$=""
51072 WD(4)=125 : REM VERTICAL EDGE
51074 WD(5)= 99 : REM HORIZONTAL EDGE
51076 WD(6)=171 : REM JOIN HORIZ TO LEFT VERT
51078 WD(7)=179 : REM JOIN HORIZ TO RIGHT VERT
51080 WD(8)=166 : REM TITLE FILL GLYPH
51082 RETURN

51084 REM ============================================
51086 REM WIN-SET-SQUARE: SQUARE CORNERS (INTERNAL)
51088 REM ============================================
51090 WD(0)=176 : REM SQUARE TL
51092 WD(1)=174 : REM SQUARE TR
51094 WD(2)=173 : REM SQUARE BL
51096 WD(3)=189 : REM SQUARE BR
51098 RETURN

51100 REM ============================================
51102 REM WIN-SET-ROUNDED: ROUNDED CORNERS (INTERNAL)
51104 REM ============================================
51106 WD(0)=117 : REM ROUNDED TL
51108 WD(1)=105 : REM ROUNDED TR
51110 WD(2)=106 : REM ROUNDED BL
51112 WD(3)=107 : REM ROUNDED BR
51114 RETURN

51122 REM ============================================
51124 REM WIN-FRAME-DRAW: DRAW WINDOW FRAME
51126 REM ============================================
51128 ER=0:ER$=""
51130 REM AN(0)=X  AN(1)=Y  AN(2)=W  AN(3)=H  AN(4)=TITLE FLAG
51132 REM AN(5)=FILL FLAG  AN(6)=CORNER FLAG (0=SQUARE,1=ROUNDED)
51134 IF AN(2)<3 OR AN(3)<4 THEN ER=2:ER$="WINDOW TOO SMALL":RETURN
51136 IF AN(6)<>0 AND AN(6)<>1 THEN ER=3:ER$="BAD CORNER FLAG":RETURN
51138 IF AN(6)=0 THEN GOSUB 51084
51140 IF AN(6)=1 THEN GOSUB 51100
51142 REM DERIVED VALUES STORED BACK INTO AN()
51144 AN(7)=AN(0)+AN(2)-1
51146 AN(8)=AN(1)+AN(3)-1
51148 AN(9)=AN(2)-2
51150 REM TOP BORDER
51152 Y=AN(1)
51154 L=WD(0):R=WD(1):M=WD(5)
51156 GOSUB 51784
51158 REM TITLE HANDLING
51160 IF AS$(0)="" AND AN(4)=0 THEN GOTO 51238
51162 REM BUILD TITLE LINE INTO T1$
51164 IF AS$(0)<>"" THEN GOTO 51176
51166 REM EMPTY TITLE BUT FORCED BAR: PURE FILL GLYPHS
51168 T1$=""
51170 FI=AN(9)
51172 FOR I=1 TO FI:T1$=T1$+CHR$(WD(8)):NEXT I
51174 GOTO 51188
51176 REM NON-EMPTY TITLE: TRUNCATE AND PAD
51178 IF LEN(AS$(0))>AN(9) THEN AS$(0)=LEFT$(AS$(0),AN(9))
51180 T1$=AS$(0)+" "
51182 FI=AN(9)-LEN(T1$)
51184 IF FI>0 THEN FOR I=1 TO FI:T1$=T1$+CHR$(WD(8)):NEXT I
51186 REM DRAW TITLE ROW
51188 YT=AN(1)+1
51190 X=AN(0)
51192 LOCATE YT,X:PRINT CHR$(WD(4));
51194 FOR I=1 TO AN(9)
51196 X=AN(0)+I
51198 C$=MID$(T1$,I,1)
51200 LOCATE YT,X:PRINT C$;
51202 NEXT I
51204 X=AN(7)
51206 LOCATE YT,X:PRINT CHR$(WD(4));
51208 REM TITLE SEPARATOR ROW
51210 Y=AN(1)+2
51212 L=WD(6):R=WD(7):M=WD(5)
51214 GOSUB 51784
51216 REM SIDE AND FILL START ROW WITH TITLE BAR
51218 SS=AN(1)+3
51220 YS=SS
51222 GOTO 51256
51238 REM NO TITLE BAR: SIDE AND FILL START JUST BELOW TOP
51240 SS=AN(1)+1
51242 YS=SS
51256 REM SIDE BORDERS
51258 FOR Y=SS TO AN(8)-1
51260 X=AN(0):LOCATE Y,X:PRINT CHR$(WD(4));
51262 X=AN(7):LOCATE Y,X:PRINT CHR$(WD(4));
51264 NEXT Y
51266 REM BOTTOM BORDER
51268 Y=AN(8)
51270 L=WD(2):R=WD(3):M=WD(5)
51272 GOSUB 51784
51274 REM OPTIONAL INTERIOR FILL
51276 IF AN(5)=0 THEN RETURN
51278 REM FILL INTERIOR WITH SPACES (CHR$(32))
51280 FOR Y=YS TO AN(8)-1
51282 FOR X=AN(0)+1 TO AN(7)-1
51284 LOCATE Y,X:PRINT CHR$(32);
51286 NEXT X
51288 NEXT Y
51290 RETURN

51782 REM ============================================
51784 REM WIN-HLINE: DRAW HORIZONTAL LINE AT Y USING L,R,M
51786 REM ============================================
51788 FOR X=AN(0) TO AN(7)
51790 IF X=AN(0) THEN C=L:GOTO 51800
51792 IF X=AN(7) THEN C=R:GOTO 51800
51794 C=M
51800 LOCATE Y,X:PRINT CHR$(C);
51802 NEXT X
51804 RETURN
