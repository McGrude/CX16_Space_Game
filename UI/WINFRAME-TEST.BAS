10 CLS
15 SW=80:SH=60
20 DIM AN(10)
30 DIM AS$(0)
45 ER=0:ER$=""

190 REM TEST WINDOW PARAMETERS (FULL SCREEN)
200 AN(0)=1
205 AN(1)=1
210 AN(2)=80
215 AN(3)=60
220 AN(4)=1
225 AN(5)=0
230 AN(6)=0
240 AS$(0)="FULL SCREEN WINDOW"
250 GOSUB 1000

255 IF ER<>0 THEN PRINT"WINDOW ERROR ";ER;": ";ER$:END
260 END    

50 REM INITIALIZE WD() GLYPHS
40 DIM WD(12)   
60 WD(0)=176 : REM SQUARE TOP LEFT CORNER
70 WD(1)=117 : REM ROUNDED TOP LEFT CORNER
80 WD(2)=174 : REM SQUARE TOP RIGHT CORNER
90 WD(3)=105 : REM ROUNDED TOP RIGHT CORNER
100 WD(4)=173 : REM SQUARE BOTTOM LEFT CORNER
110 WD(5)=106 : REM ROUNDED BOTTOM LEFT CORNER
120 WD(6)=189 : REM SQUARE BOTTOM RIGHT CORNER
130 WD(7)=107 : REM ROUNDED BOTTOM RIGHT CORNER
140 WD(8)=125 : REM VERTICAL EDGE
150 WD(9)= 99 : REM HORIZONTAL EDGE
160 WD(10)=171: REM JOIN HORIZONTAL TO LEFT VERTICAL
170 WD(11)=179: REM JOIN HORIZONTAL TO RIGHT VERTICAL
180 WD(12)=166: REM TITLE BAR FILL

1000 REM DRAW WINDOW USING AN(), AS$(), WD()
1005 ER=0:ER$=""
1010 X0=AN(0):Y0=AN(1):W=AN(2):H=AN(3)
1015 X1=X0+W-1:Y1=Y0+H-1:TW=W-2
1020 FG=AN(4):BG=AN(5):RC=AN(6)

1030 ER=0:ER$=""

1040 IF RC<>0 AND RC<>1 THEN ER=1:ER$="BAD CORNER TYPE":RETURN
1050 IF W<3 OR H<4 THEN ER=2:ER$="WINDOW TOO SMALL":RETURN
1060 IF FG<0 OR FG>15 OR BG<0 OR BG>15 THEN ER=3:ER$="BAD COLOR":RETURN

1070 COLOR FG,BG

1080 REM TOP BORDER
1090 Y=Y0:L=WD(0+RC):R=WD(2+RC):M=WD(9):GOSUB 3000

1100 REM TITLE ROW
1110 YT=Y0+1
1120 IF YT<1 OR YT>SH THEN 1270

1130 T$=AS$(0)
1140 IF LEN(T$)>TW THEN T$=LEFT$(T$,TW)

1150 REM BUILD TITLE+SPACE+FILL STRING
1160 FI=TW-LEN(T$)
1170 T1$=T$+" "
1180 FI=FI-1
1190 IF FI>0 THEN FOR I=1 TO FI:T1$=T1$+CHR$(WD(12)):NEXT I

1200 REM LEFT TITLE BORDER (VERTICAL)
1210 X=X0
1220 IF X>=1 AND X<=SW THEN LOCATE YT,X:PRINT CHR$(WD(8));

1230 FOR I=1 TO TW
1240 X=X0+I
1250 IF X>=1 AND X<=SW THEN C$=MID$(T1$,I,1):LOCATE YT,X:PRINT C$;
1260 NEXT I

1265 X=X1
1266 IF X>=1 AND X<=SW THEN LOCATE YT,X:PRINT CHR$(WD(8));

1270 REM TITLE SEPARATOR ROW
1280 Y=Y0+2:L=WD(10):R=WD(11):M=WD(9):GOSUB 3000

1290 REM SIDE BORDERS ONLY
1300 FOR Y=Y0+3 TO Y1-1
1310 IF Y<1 OR Y>SH THEN 1360
1320 X=X0
1330 IF X>=1 AND X<=SW THEN LOCATE Y,X:PRINT CHR$(WD(8));
1340 X=X1
1350 IF X>=1 AND X<=SW THEN LOCATE Y,X:PRINT CHR$(WD(8));
1360 NEXT Y

1370 REM BOTTOM BORDER
1380 Y=Y1:L=WD(4+RC):R=WD(6+RC):M=WD(9):GOSUB 3000

1390 RETURN

3000 REM DRAW HORIZONTAL LINE AT Y WITH L,R,M CHARS
3010 IF Y<1 OR Y>SH THEN RETURN
3020 FOR X=X0 TO X1
3030 IF X<1 OR X>SW THEN 3080
3040 IF X=X0 THEN C=L:GOTO 3070
3050 IF X=X1 THEN C=R:GOTO 3070
3060 C=M
3070 LOCATE Y,X:PRINT CHR$(C);
3080 NEXT X
3090 RETURN
